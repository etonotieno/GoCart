static def isLinuxOrMacOs() {
    def osName = System.getProperty("os.name").toLowerCase(Locale.ROOT)
    return osName.contains("linux") || osName.contains("mac os") || osName.contains("macos")
}

tasks.register('copyGitHooks', Copy) {
    description "Copies the git hooks from /git-hooks to the .git folder."
    from("$rootDir/scripts/git-hooks/") {
        include "**/*.sh"
        rename "(.*).sh", "\$1"
    }
    into "${rootDir}/.git/hooks"
    onlyIf { isLinuxOrMacOs() }
}

tasks.register('installGitHooks', Exec) {
    description = "Installs the pre-commit git hooks from scripts/git-hooks."
    group "git hooks"
    workingDir rootDir
    commandLine "chmod"
    args "-R", "+x", ".git/hooks/"
    dependsOn copyGitHooks
    onlyIf { isLinuxOrMacOs() }
    doLast {
        logger.info("Git hook installed successfully.")
    }
}
